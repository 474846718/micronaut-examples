import com.amazonaws.services.lambda.model.InvocationType
import jp.classmethod.aws.gradle.lambda.AWSLambdaInvokeTask
import jp.classmethod.aws.gradle.lambda.AWSLambdaMigrateFunctionTask
import com.amazonaws.services.lambda.model.Runtime
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.22"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id "de.sebastianboegl.shadow.transformer.log4j" version "2.1.1"
    id 'java'
    id "application"
}

apply plugin: 'jp.classmethod.aws.lambda'
apply plugin:"io.spring.dependency-management"

mainClassName = "example.HelloWorldSkill"
applicationDefaultJvmArgs = [""]

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}
repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.1.0.BUILD-SNAPSHOT'
    }
}

dependencies {
    compileOnly "io.micronaut:micronaut-inject-java"
    compile "io.micronaut:micronaut-inject-groovy"
    //compile "io.micronaut:function-groovy:${projectVersion}"
    compile "com.amazon.alexa:ask-sdk:2.2.1"
    compile "io.micronaut:micronaut-function-aws-alexa"
    runtime "com.amazonaws:aws-lambda-java-log4j2:1.0.0"
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'


    testCompile "io.micronaut:micronaut-function-client"
    testRuntime "io.micronaut:micronaut-http-server-netty"
    testRuntime "io.micronaut:micronaut-function-web"
    testRuntime group: 'com.amazonaws', name: 'aws-java-sdk-lambda', version: '1.11.285'

    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude module:'groovy-all'
    }

}
shadowJar {
    mergeServiceFiles()
}

task deploy(type: AWSLambdaMigrateFunctionTask, dependsOn: shadowJar) {
    functionName = "hello-world-alexa-java"
    handler = "example.HelloWorldSkill::apply"
    role = "arn:aws:iam::${aws.accountId}:role/lambda_basic_execution"
    runtime = Runtime.Java8
    zipFile = shadowJar.archivePath
    memorySize = 256
    timeout = 60
}

task invoke(type: AWSLambdaInvokeTask) {
    functionName = "hello-world-alexa-java"
    invocationType = InvocationType.RequestResponse
    payload = '{"name":"Fred"}'
    doLast {
        println "Lambda function result: " + new String(invokeResult.payload.array(), "UTF-8")
    }
}
